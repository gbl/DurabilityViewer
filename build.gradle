buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.3+1.16'
    }
    repositories {
	maven {
        name = 'CrowdinTranslate source'
		url = "https://minecraft.guntram.de/maven/"
	}
    }
}
plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
}
apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'durabilityviewer'
crowdintranslate.verbose = false

repositories {
	maven {
		url = "https://maven.fabricmc.net/"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	maven {
		url = "file:///tmp/mymavenrepo/"
	}
	maven {
		url = "https://jitpack.io"
	}
	maven {
		name = "NerdHubMC"
		url = "https://maven.abusedmaster.xyz/"
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-21w05a.properties").newReader())

archivesBaseName = "durabilityviewer"
ext.projectVersion = "1.10"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

minecraft {
    refmapName = "DurabilityViewer-refmap.json";
}

processResources {
	inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "TechReborn:TechReborn-1.16:3.4.5+build.95"
    modImplementation "io.github.prospector:modmenu:${Versions['modmenu_version']}"
    modImplementation "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    include    "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    
	modImplementation "com.github.emilyploszaj:trinkets:v2.4.1"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

processResources {
	dependsOn downloadTranslations
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'LTM1f0yY'
    versionNumber = project.version
    versionName = project.archivesBaseName
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}

